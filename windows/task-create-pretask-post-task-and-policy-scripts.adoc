---
permalink: windows/task-create-pretask-post-task-and-policy-scripts.html 
sidebar: sidebar 
keywords: snapmanager, enable, create, script, preprocess, activity, post-process, policy, task, backup, restore, clone, operation, installation, directory, pretask, post-task 
summary: SnapManager를 사용하면 전처리 작업, 사후 처리 작업 및 백업, 복원 및 클론 작업의 정책 작업에 대한 스크립트를 생성할 수 있습니다. SnapManager 작업의 전처리 작업, 후처리 작업 및 정책 작업을 실행하려면 올바른 설치 디렉토리에 스크립트를 배치해야 합니다. 
---
= 사전 작업, 사후 작업 및 정책 스크립트를 생성합니다
:allow-uri-read: 
:icons: font
:imagesdir: ../media/


[role="lead"]
SnapManager를 사용하면 전처리 작업, 사후 처리 작업 및 백업, 복원 및 클론 작업의 정책 작업에 대한 스크립트를 생성할 수 있습니다. SnapManager 작업의 전처리 작업, 후처리 작업 및 정책 작업을 실행하려면 올바른 설치 디렉토리에 스크립트를 배치해야 합니다.



== 이 작업에 대해

* 사전 작업 및 사후 작업 스크립트 내용 *

모든 스크립트에는 다음이 포함되어야 합니다.

* 특정 작업(점검, 설명, 실행)
* (선택 사항) 사전 정의된 환경 변수
* 특정 오류 처리 코드(리턴 코드(rc))



NOTE: 스크립트의 유효성을 검사하려면 올바른 오류 처리 코드를 포함해야 합니다.

SnapManager 작업이 시작되기 전에 디스크 공간을 정리하는 등의 여러 가지 용도로 사전 작업 스크립트를 사용할 수 있습니다. 예를 들어 사후 작업 스크립트를 사용하여 SnapManager에 작업을 완료할 수 있는 충분한 디스크 공간이 있는지 추정할 수 있습니다.

* 정책 작업 스크립트 내용 *

확인, 설명, 실행 등의 특정 작업을 사용하지 않고 정책 스크립트를 실행할 수 있습니다. 이 스크립트에는 사전 정의된 환경 변수(선택 사항)와 특정 오류 처리 코드가 포함되어 있습니다.

정책 스크립트는 백업, 복원 및 클론 작업 전에 실행됩니다.

* 지원되는 형식 *

명령 파일 확장명이 .cmd인 경우 처방과 사후 스크립트로 사용할 수 있습니다.


NOTE: 쉘 스크립트 파일을 선택하면 SnapManager 작업이 응답하지 않습니다. 이 문제를 해결하려면 플러그인 디렉토리에 명령 파일을 제공하고 SnapManager 작업을 다시 수행해야 합니다.

* 스크립트 설치 디렉토리 *

스크립트를 설치하는 디렉터리는 사용 방식에 영향을 줍니다. 스크립트를 디렉토리에 배치하고 백업, 복원 또는 클론 작업이 실행되기 전이나 후에 스크립트를 실행할 수 있습니다. 테이블에 지정된 디렉토리에 스크립트를 배치하고 백업, 복원 또는 클론 작업을 지정할 때 선택적으로 사용해야 합니다.


NOTE: SnapManager 작업에 스크립트를 사용하기 전에 플러그인 디렉토리에 실행 가능한 권한이 있는지 확인해야 합니다.

[cols="4*"]
|===
| 활동입니다 | 백업 | 복원 | 복제 


 a| 
사전 처리
 a| 
default_installation_directory>\plugins\backup\create\pre
 a| 
default_installation_directory>\plugins\restore\create\pre를 선택합니다
 a| 
default_installation_directory>\plugins\clone\create\pre



 a| 
후처리
 a| 
default_installation_directory>\plugins\backup\create\post를 선택합니다
 a| 
default_installation_directory>\plugins\restore\create\post 를 참조하십시오
 a| 
default_installation_directory>\plugins\clone\create\post 를 선택합니다



 a| 
정책 기반
 a| 
default_installation_directory>\plugins\backup\create\policy
 a| 
default_installation_directory>\plugins\restore\create\policy
 a| 
default_installation_directory>\plugins\clone\create\policy

|===
* 샘플 스크립트 위치 *

다음은 설치 디렉토리 경로에서 사용할 수 있는 백업 및 클론 작업을 위한 사전 작업 스크립트 및 사후 작업 스크립트의 몇 가지 샘플입니다.

* default_installation_directory>\plugins\examples\backup\create\pre
* default_installation_directory>\plugins\examples\backup\create\post를 선택합니다
* default_installation_directory>\plugins\examples\clone\create\pre
* default_installation_directory>\plugins\examples\clone\create\post를 선택합니다


* 스크립트에서 변경할 수 있는 사항 *

새 스크립트를 만드는 경우 설명 및 실행 작업만 변경할 수 있습니다. 각 스크립트에는 '컨텍스트', '시간 초과', '매개 변수' 등의 변수가 포함되어야 합니다.

스크립트의 설명 함수에 설명된 변수는 스크립트 시작 시에 선언되어야 합니다. 매개 변수=()'에 새 매개 변수 값을 추가한 다음 execute 함수에 매개 변수를 사용할 수 있습니다.



== 샘플 스크립트

다음은 SnapManager 호스트의 공간을 추정하기 위한 사용자 지정 반환 코드가 있는 샘플 스크립트입니다.

[listing]
----
@echo off
REM $Id: //depot/prod/capstan/Rcapstan_ganges/src/plugins/windows/examples/clone/create/policy/validate_sid.cmd#1 $
REM $Revision: #1 $ $Date: 2011/12/06 $
REM
REM

set /a EXIT=0

set name="Validate SID"
set description="Validate SID used on the target system"
set parameter=()

rem reserved system IDs
set INVALID_SIDS=("ADD" "ALL" "AND" "ANY" "ASC" "COM" "DBA" "END" "EPS" "FOR" "GID" "IBM" "INT" "KEY" "LOG" "MON" "NIX" "NOT" "OFF" "OMS" "RAW" "ROW" "SAP" "SET" "SGA" "SHG" "SID" "SQL" "SYS" "TMP" "UID" "USR" "VAR")

if /i "%1" == "-check" goto :check
if /i "%1" == "-execute" goto :execute
if /i "%1" == "-describe" goto :describe

:usage:
	echo usage: %0 "{ -check | -describe | -execute }"
	set /a EXIT=99
	goto :exit

:check
	set /a EXIT=0
	goto :exit

:describe
	echo SM_PI_NAME:%name%
	echo SM_PI_DESCRIPTION:%description%
	set /a EXIT=0
	goto :exit

:execute
	set /a EXIT=0

	rem SM_TARGET_SID must be set
	if "%SM_TARGET_SID%" == "" (
		set /a EXIT=4
		echo SM_TARGET_SID not set
		goto :exit
	)

	rem exactly three alphanumeric characters, with starting with a letter
	echo %SM_TARGET_SID% | findstr "\<[a-zA-Z][a-zA-Z0-9][a-zA-Z0-9]\>" >nul
	if %ERRORLEVEL% == 1 (
		set /a EXIT=4
		echo SID is defined as a 3 digit value starting with a letter. [%SM_TARGET_SID%] is not valid.
		goto :exit
	)

	rem not a SAP reserved SID
	echo %INVALID_SIDS% | findstr /i \"%SM_TARGET_SID%\" >nul
	if %ERRORLEVEL% == 0 (
		set /a EXIT=4
		echo SID [%SM_TARGET_SID%] is reserved by SAP
		goto :exit
	)

	goto :exit



:exit
	echo Command complete.
	exit /b %EXIT%
----